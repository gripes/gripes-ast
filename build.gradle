apply {
	plugin 'groovy'
	plugin 'eclipse'
}

logger.quiet "Building Gripersist"

group = 'gripes'
archivesBaseName = 'gripersist'
version = '0.1.3'

versions = [
	'gripes-web' : '0.1.7',
	'gripes-plugin' : '0.1.7',
	'gripersist' : '0.1.1',
	'hibernate-core' : '4.0.0.Beta2'
]

repositories {
    mavenCentral()
    mavenRepo urls: "http://www.gripes-project.org/libs"
    mavenRepo name: "jboss-snapshots", urls: "http://snapshots.jboss.org/maven2/"
}

configurations {
	ftpAntTask {}
	provided {}
}

sourceSets {
	main {
		compileClasspath += configurations.provided
		groovy { srcDir 'src' }
	    resources { srcDir 'resources' }
	}	
	test {
		groovy { srcDir 'test' }
	    resources { srcDir 'test' }
	}	
}

dependencies {
	//** Ant FTP
	ftpAntTask module("org.apache.ant:ant-commons-net:1.7.0") {
	    module("commons-net:commons-net:1.4.1") {
	        dependencies("oro:oro:2.0.8@jar")
	    }
	}
	
	//** Groovy
	groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.10'
	compile group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.10'
	
	//** Servlet
	provided group: 'javax.servlet', name: 'servlet-api', version: '2.5'

	//** Framework
	compile group: 'net.sourceforge.stripes', name: 'stripes', version: '1.5.6'
	
	//** Database
	compile group: 'org.hibernate', name: 'hibernate-core', version: versions['hibernate-core']
	compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: versions['hibernate-core']
	runtime group: 'org.hibernate', name: 'hibernate-c3p0', version: versions['hibernate-core']
	
	//** Logging
	provided group: 'org.slf4j', name: 'slf4j-api', version: '1.6.1'
	provided group: 'ch.qos.logback', name: 'logback-core', version: '0.9.28'
	provided group: 'ch.qos.logback', name: 'logback-access', version: '0.9.28'
	provided group: 'ch.qos.logback', name: 'logback-classic', version: '0.9.28'
	
	//** Testing
	testCompile 'junit:junit:4.10'
}

task eclipseCp(dependsOn: eclipseClasspath) {}

uploadArchives {
    repositories {
       flatDir(dirs: file('repos'))
    }
}
uploadArchives.doLast {
	/*	
	ant {
		copy(todir:buildDir.canonicalPath+"/"+libsDirName) {
	    	fileset(dir:buildDir.canonicalPath, includes:"ivy.xml")
		}
		delete(file: buildDir.canonicalPath+"/ivy.xml")
    }
    */
    
	ant {
		taskdef(name: 'ftp',
                classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
                classpath: configurations.ftpAntTask.asPath)

		ftp(action: "mkdir",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripersist/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword)
			
		ftp(action: "put",
			server: "www.gripes-project.org", 
			remotedir: "${ftpUploadRoot}/libs/gripes/gripersist/${version}",
			chmod: "0755",
			userid: ftpUploadUser,
			password: ftpUploadPassword) {
			fileset(dir:"repos", includes: "*.*")
		}
    }
}